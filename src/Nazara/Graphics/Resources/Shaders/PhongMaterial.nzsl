[nzsl_version("1.0")]
module PhongMaterial;

import InstanceData from Engine.InstanceData;
import LightData from Engine.LightData;
import SkeletalData from Engine.SkeletalData;
import ViewerData from Engine.ViewerData;

import * from Engine.LightShadow;
import SkinLinearPosition, SkinLinearPositionNormal from Engine.SkinningLinear;

// Pass-specific options
option DepthPass: bool = false;
option DistanceDepth: bool = false;
option ShadowPass: bool = false;

// Basic material options
option HasBaseColorTexture: bool = false;
option HasAlphaTexture: bool = false;
option AlphaTest: bool = false;

// Phong material options
option HasEmissiveTexture: bool = false;
option HasHeightTexture: bool = false;
option HasNormalTexture: bool = false;
option HasSpecularTexture: bool = false;

// Billboard related options
option Billboard: bool = false;

// Vertex declaration related options
option VertexColorLoc: i32 = -1;
option VertexNormalLoc: i32 = -1;
option VertexPositionLoc: i32 = -1;
option VertexSizeRotLocation: i32 = -1;
option VertexTangentLoc: i32 = -1;
option VertexUvLoc: i32 = -1;

option VertexJointIndicesLoc: i32 = -1;
option VertexJointWeightsLoc: i32 = -1;

option MaxLightCount: u32 = u32(3); //< FIXME: Fix integral value types

const HasNormal = (VertexNormalLoc >= 0);
const HasVertexColor = (VertexColorLoc >= 0);
const HasColor = (HasVertexColor || Billboard);
const HasTangent = (VertexTangentLoc >= 0);
const HasVertexUV = (VertexUvLoc >= 0);
const HasUV = (HasVertexUV);
const HasNormalMapping = HasNormalTexture && HasNormal && HasTangent && !DepthPass;
const HasSkinning = (VertexJointIndicesLoc >= 0 && VertexJointWeightsLoc >= 0);
const HasLighting = HasNormal && !DepthPass;

[layout(std140)]
struct MaterialSettings
{
	// Basic settings
	[tag("AlphaTestThreshold")]
	AlphaThreshold: f32,

	[tag("ShadowMapNormalOffset")]
	ShadowMapNormalOffset: f32,

	[tag("ShadowPosScale")]
	ShadowPosScale: f32,

	[tag("BaseColor")]
	BaseColor: vec4[f32],

	// Phong settings
	[tag("AmbientColor")]
	AmbientColor: vec4[f32], //< TODO: Switch to vec3[f32]

	[tag("SpecularColor")]
	SpecularColor: vec4[f32], //< TODO: Switch to vec3[f32

	[tag("Shininess")]
	Shininess: f32
}

// TODO: Add enums
const DirectionalLight = 0;
const PointLight = 1;
const SpotLight = 2;

[tag("Material")]
[auto_binding]
external
{
	[tag("Settings")] settings: uniform[MaterialSettings],
	[tag("BaseColorMap")] MaterialBaseColorMap: sampler2D[f32],
	[tag("AlphaMap")] MaterialAlphaMap: sampler2D[f32],
	[tag("EmissiveMap")] MaterialEmissiveMap: sampler2D[f32],
	[tag("HeightMap")] MaterialHeightMap: sampler2D[f32],
	[tag("NormalMap")] MaterialNormalMap: sampler2D[f32],
	[tag("SpecularMap")] MaterialSpecularMap: sampler2D[f32],
}

[tag("Engine")]
[auto_binding]
external
{
	[tag("TextureOverlay")] TextureOverlay: sampler2D[f32],
	[tag("InstanceData")] instanceData: uniform[InstanceData],
	[tag("ViewerData")] viewerData: uniform[ViewerData],
	[tag("SkeletalData")] skeletalData: uniform[SkeletalData],
	[tag("LightData")] lightData: uniform[LightData],
	[tag("ShadowMapsDirectional")] shadowMapsDirectional: array[depth_sampler2D_array[f32], MaxLightCount],
	[tag("ShadowMapsPoint")] shadowMapsPoint: array[sampler_cube[f32], MaxLightCount],
	[tag("ShadowMapsSpot")] shadowMapsSpot: array[depth_sampler2D[f32], MaxLightCount],
}

struct VertOut
{
	[location(0)] worldPos: vec3[f32],
	[location(1), cond(HasUV)] uv: vec2[f32],
	[location(2), cond(HasColor)] color: vec4[f32],
	[location(3), cond(HasNormal)] normal: vec3[f32],
	[location(4), cond(HasNormalMapping)] tangent: vec3[f32],
	[builtin(position)] position: vec4[f32],
	[builtin(frag_coord)] fragcoord: vec4[f32]
}

// Fragment stage
struct FragOut
{
	[location(0)] RenderTarget0: vec4[f32],
	[builtin(frag_depth), cond(DistanceDepth)] fragdepth: f32,
}

fn ComputeColor(input: VertOut) -> vec4[f32]
{
	let color = settings.BaseColor;

	const if (HasUV)
		color.a *= TextureOverlay.Sample(input.uv).r;

	const if (HasColor)
		color *= input.color;

	const if (HasBaseColorTexture)
		color *= MaterialBaseColorMap.Sample(input.uv);

	const if (HasAlphaTexture)
		color.w *= MaterialAlphaMap.Sample(input.uv).x;

	const if (AlphaTest)
	{
		if (color.w < settings.AlphaThreshold)
			discard;
	}

	return color;
}

[cond(HasLighting)]
fn ComputeLighting(color: vec3[f32], input: VertOut) -> vec3[f32]
{
	let lightAmbient = vec3[f32](0.0, 0.0, 0.0);
	let lightDiffuse = vec3[f32](0.0, 0.0, 0.0);
	let lightSpecular = vec3[f32](0.0, 0.0, 0.0);

	let eyeVec = normalize(viewerData.eyePosition - input.worldPos);

	let normal: vec3[f32];
	const if (HasNormalMapping)
	{
		let N = normalize(input.normal);
		let T = normalize(input.tangent);
		let B = cross(N, T);
		let tbnMatrix = mat3[f32](T, B, N);

		normal = normalize(tbnMatrix * (MaterialNormalMap.Sample(input.uv).xyz * 2.0 - vec3[f32](1.0, 1.0, 1.0)));
	}
	else
		normal = normalize(input.normal);

	for lightIndex in u32(0) -> lightData.directionalLightCount
	{
		let light = lightData.directionalLights[lightIndex];

		let lambert = max(dot(normal, -light.direction), 0.0);

		let reflection = reflect(light.direction, normal);
		let specFactor = max(dot(reflection, eyeVec), 0.0);
		specFactor = pow(specFactor, settings.Shininess);

		let shadowFactor = ComputeDirectionalLightShadow(light, shadowMapsDirectional[lightIndex], input.worldPos, lambert, viewerData.viewMatrix);

		lightAmbient += shadowFactor * light.color.rgb * light.ambientFactor * settings.AmbientColor.rgb;
		lightDiffuse += shadowFactor * lambert * light.color.rgb * light.diffuseFactor;
		lightSpecular += shadowFactor * specFactor * light.color.rgb;
	}

	for lightIndex in u32(0) -> lightData.pointLightCount
	{
		let light = lightData.pointLights[lightIndex];

		let lightToPos = input.worldPos - light.position;
		let dist = length(lightToPos);
		let lightToPosNorm = lightToPos / max(dist, 0.0001);

		let attenuationFactor = max(1.0 - dist * light.invRadius, 0.0);
		let lambert = clamp(dot(normal, -lightToPosNorm), 0.0, 1.0);

		let reflection = reflect(lightToPosNorm, normal);
		let specFactor = max(dot(reflection, eyeVec), 0.0);
		specFactor = pow(specFactor, settings.Shininess);

		let shadowFactor = ComputePointLightShadow(light, shadowMapsPoint[lightIndex], dist, lightToPosNorm);

		lightAmbient += attenuationFactor * light.color.rgb * light.ambientFactor * settings.AmbientColor.rgb;
		lightDiffuse += shadowFactor * attenuationFactor * lambert * light.color.rgb * light.diffuseFactor;
		lightSpecular += shadowFactor * attenuationFactor * specFactor * light.color.rgb;
	}

	for lightIndex in u32(0) -> lightData.spotLightCount
	{
		let light = lightData.spotLights[lightIndex];

		let lightToPos = input.worldPos - light.position;
		let dist = length(lightToPos);
		let lightToPosNorm = lightToPos / max(dist, 0.0001);

		let curAngle = dot(light.direction, lightToPosNorm);
		let innerMinusOuterAngle = light.innerAngle - light.outerAngle;

		let attenuationFactor = max(1.0 - dist * light.invRadius, 0.0);
		attenuationFactor *= max((curAngle - light.outerAngle) / innerMinusOuterAngle, 0.0);			

		let lambert = clamp(dot(normal, -lightToPosNorm), 0.0, 1.0);

		let reflection = reflect(lightToPosNorm, normal);
		let specFactor = max(dot(reflection, eyeVec), 0.0);
		specFactor = pow(specFactor, settings.Shininess);

		let shadowFactor = ComputeSpotLightShadow(light, shadowMapsSpot[lightIndex], input.worldPos, lambert);

		lightAmbient += attenuationFactor * light.color.rgb * light.ambientFactor * settings.AmbientColor.rgb;
		lightDiffuse += shadowFactor * attenuationFactor * lambert * light.color.rgb * light.diffuseFactor;
		lightSpecular += shadowFactor * attenuationFactor * specFactor * light.color.rgb;
	}

	lightSpecular *= settings.SpecularColor.rgb;

	const if (HasSpecularTexture)
		lightSpecular *= MaterialSpecularMap.Sample(input.uv).rgb;

	let lightColor = lightAmbient + lightDiffuse + lightSpecular;
	return color * lightColor;
}

[entry(frag), cond(!DepthPass)]
fn FragMain(input: VertOut) -> FragOut
{
	let color = ComputeColor(input);

	const if (HasLighting)
		color.rgb = ComputeLighting(color.rgb, input);
	
	let output: FragOut;
	output.RenderTarget0 = color;

	return output;
}

// Shadow passes entries
[entry(frag), cond(DepthPass && DistanceDepth)]
[depth_write(replace)]
fn FragDepthDist(input: VertOut) -> FragOut
{
	let color = ComputeColor(input);

	let output: FragOut;
	output.RenderTarget0 = color;

	let dist = distance(viewerData.eyePosition, input.worldPos);
	output.fragdepth = dist / viewerData.farPlane;

	return output;
}

[entry(frag), cond(DepthPass && AlphaTest && !DistanceDepth)]
fn FragDepth(input: VertOut) -> FragOut
{
	let color = ComputeColor(input);

	let output: FragOut;
	output.RenderTarget0 = color;
	return output;
}

[entry(frag), cond(DepthPass && !AlphaTest && !DistanceDepth)]
fn FragDepthNoAlpha() {} //< dummy

// Vertex stage
[cond(!Billboard)]
struct VertIn
{
	[location(VertexPositionLoc)]
	pos: vec3[f32],

	[cond(HasVertexColor), location(VertexColorLoc)]
	color: vec4[f32],

	[cond(HasVertexUV), location(VertexUvLoc)]
	uv: vec2[f32],

	[cond(HasNormal), location(VertexNormalLoc)]
	normal: vec3[f32],

	[cond(HasTangent), location(VertexTangentLoc)]
	tangent: vec3[f32],

	[cond(HasSkinning), location(VertexJointIndicesLoc)]
	jointIndices: vec4[i32],

	[cond(HasSkinning), location(VertexJointWeightsLoc)]
	jointWeights: vec4[f32]
}

[cond(Billboard)]
struct BillboardVertIn
{
	[builtin(vertex_index)] vertIndex: i32,

	[location(VertexSizeRotLocation)]
	sizeRot: vec4[f32], //< width,height,sin,cos

	[cond(HasVertexUV), location(VertexUvLoc)]
	uv: vec2[f32],

	[cond(HasVertexColor), location(VertexColorLoc)]
	color: vec4[f32]
}

const billboardPos = array[vec2[f32]](
	vec2[f32](-0.5, -0.5),
	vec2[f32]( 0.5, -0.5),
	vec2[f32](-0.5, 0.5),
	vec2[f32]( 0.5, 0.5)
);

[entry(vert), cond(Billboard)]
fn VertBillboard(input: BillboardVertIn) -> VertOut
{
	let position = billboardPos[input.vertIndex % 4];

	let size = input.sizeRot.xy;
	let sinCos = input.sizeRot.zw;

	let rotatedPosition = vec2[f32](
		position.x * sinCos.y - position.y * sinCos.x,
		position.y * sinCos.y + position.x * sinCos.x
	);
	rotatedPosition *= size;

	let cameraRight = vec3[f32](viewerData.viewMatrix[0][0], viewerData.viewMatrix[1][0], viewerData.viewMatrix[2][0]);
	let cameraUp = vec3[f32](viewerData.viewMatrix[0][1], viewerData.viewMatrix[1][1], viewerData.viewMatrix[2][1]);

	let worldPosition = vec3[f32](instanceData.worldMatrix[3].xyz);
	worldPosition += cameraRight * rotatedPosition.x;
	worldPosition += cameraUp * rotatedPosition.y;

	let output: VertOut;
	output.worldPos = worldPosition;
	output.position = viewerData.viewProjMatrix * vec4[f32](worldPosition, 1.0);

	const if (HasVertexUV)
		output.uv = input.uv;

	const if (HasColor)
		output.color = input.color;

	return output;
}

[entry(vert), cond(!Billboard)]
fn main(input: VertIn) -> VertOut
{
	let pos: vec3[f32];
	const if (HasNormal) let normal: vec3[f32];

	const if (HasSkinning)
	{
		let jointMatrices = array[mat4[f32]](
			skeletalData.jointMatrices[input.jointIndices[0]],
			skeletalData.jointMatrices[input.jointIndices[1]],
			skeletalData.jointMatrices[input.jointIndices[2]],
			skeletalData.jointMatrices[input.jointIndices[3]]
		);

		const if (HasNormal)
		{
			let skinningOutput = SkinLinearPositionNormal(jointMatrices, input.jointWeights, input.pos, input.normal);
			pos = skinningOutput.position;
			normal = skinningOutput.normal;
		}
		else
		{
			let skinningOutput = SkinLinearPosition(jointMatrices, input.jointWeights, input.pos);
			pos = skinningOutput.position;
		}
	}
	else
	{
		pos = input.pos;
		const if (HasNormal)
			normal = input.normal;
	}

	const if (ShadowPass)
	{
		pos *= settings.ShadowPosScale;
		const if (HasNormal)
			pos -= input.normal * settings.ShadowMapNormalOffset;
	}

	let worldPosition = instanceData.worldMatrix * vec4[f32](pos, 1.0);

	let output: VertOut;
	output.worldPos = worldPosition.xyz;
	output.position = viewerData.viewProjMatrix * worldPosition;

	const if (HasNormal || HasNormalMapping) let rotationMatrix = transpose(inverse(mat3[f32](instanceData.worldMatrix)));

	const if (HasColor)
		output.color = input.color;

	const if (HasNormal)
		output.normal = rotationMatrix * normal;

	const if (HasVertexUV)
		output.uv = input.uv;

	const if (HasNormalMapping)
		output.tangent = rotationMatrix * input.tangent;

	return output;
}
