[nzsl_version("1.1")]
module PhysicallyBasedMaterial;

import InstanceData from Engine.InstanceData;
import LightData from Lighting.LightData;
import SkeletalData from Engine.SkeletalData;
import ViewerData from Engine.ViewerData;

import * from Lighting.PhysicalledBased;
import * from Lighting.Shadow;
import Pi from Math.Constants;
import DirectionalLight, PointLight, SpotLight from Lighting.LightData;
import SkinLinearPosition, SkinLinearPositionNormal from Engine.SkinningLinear;

import GBufferOutput from DeferredShading.PhysicallyBasedGBuffer;
import EncodeNormal from DeferredShading.NormalEncoding;

// Pass-specific options
option DepthPass: bool = false;
option DistanceDepth: bool = false;
option ForwardPass: bool = true;
option ShadowPass: bool = false;

// Basic material options
option HasBaseColorTexture: bool = false;
option HasAlphaTexture: bool = false;
option AlphaTest: bool = false;

// Physically-based material options
option HasEmissiveTexture: bool = false;
option HasHeightTexture: bool = false;
option HasMetallicTexture: bool = false;
option HasNormalTexture: bool = false;
option HasRoughnessTexture: bool = false;
option HasSpecularTexture: bool = false;

// Billboard related options
option Billboard: bool = false;

// Vertex declaration related options
const InvalidLoc = u32.Max;

option VertexColorLoc: u32 = InvalidLoc;
option VertexNormalLoc: u32 = InvalidLoc;
option VertexPositionLoc: u32 = InvalidLoc;
option VertexSizeRotLocation: u32 = InvalidLoc;
option VertexTangentLoc: u32 = InvalidLoc;
option VertexUvLoc: u32 = InvalidLoc;

option VertexJointIndicesLoc: u32 = InvalidLoc;
option VertexJointWeightsLoc: u32 = InvalidLoc;

option EnableShadowMapping: bool = true;
option MaxLightCount: u32 = 3;

const HasNormal = (VertexNormalLoc != InvalidLoc);
const HasVertexColor = (VertexColorLoc != InvalidLoc);
const HasColor = (HasVertexColor || Billboard);
const HasTangent = (VertexTangentLoc != InvalidLoc);
const HasVertexUV = (VertexUvLoc != InvalidLoc);
const HasUV = (HasVertexUV);
const HasNormalMapping = HasNormalTexture && HasNormal && HasTangent && !DepthPass;
const HasSkinning = (VertexJointIndicesLoc != InvalidLoc && VertexJointWeightsLoc != InvalidLoc);
const HasLighting = HasNormal && !DepthPass;

[layout(std140)]
struct MaterialSettings
{
	// Basic settings
	[tag("AlphaTestThreshold")]
	AlphaThreshold: f32,

	[tag("ShadowMapNormalOffset")]
	ShadowMapNormalOffset: f32,

	[tag("ShadowPosScale")]
	ShadowPosScale: f32,

	[tag("BaseColor")]
	BaseColor: vec4[f32],

	// PBR settings
	[tag("MetallicFactor")]
	MetallicFactor: f32,

	[tag("RoughnessFactor")]
	RoughnessFactor: f32
}

[tag("Material")]
[auto_binding]
external
{
	[tag("Settings")] settings: uniform[MaterialSettings],
	[tag("BaseColorMap")] MaterialBaseColorMap: sampler2D[f32],
	[tag("AlphaMap")] MaterialAlphaMap: sampler2D[f32],
	[tag("EmissiveMap")] MaterialEmissiveMap: sampler2D[f32],
	[tag("HeightMap")] MaterialHeightMap: sampler2D[f32],
	[tag("MetallicMap")] MaterialMetallicMap: sampler2D[f32],
	[tag("NormalMap")] MaterialNormalMap: sampler2D[f32],
	[tag("RoughnessMap")] MaterialRoughnessMap: sampler2D[f32],
	[tag("SpecularMap")] MaterialSpecularMap: sampler2D[f32],
}

[tag("Engine")]
[auto_binding]
external
{
	[tag("TextureOverlay")] TextureOverlay: sampler2D[f32],
	[tag("InstanceData")] instanceData: uniform[InstanceData],
	[tag("ViewerData")] viewerData: uniform[ViewerData],
	[tag("SkeletalData")] skeletalData: uniform[SkeletalData],
	[tag("LightData")] lightData: uniform[LightData],
	[tag("ShadowMapsDirectional")] shadowMapsDirectional: array[depth_sampler2D_array[f32], MaxLightCount],
	[tag("ShadowMapsPoint")] shadowMapsPoint: array[sampler_cube[f32], MaxLightCount],
	[tag("ShadowMapsSpot")] shadowMapsSpot: array[depth_sampler2D[f32], MaxLightCount],
}

[export]
struct VertOut
{
	[location(0)] worldPos: vec3[f32],
	[location(1), cond(HasUV)] uv: vec2[f32],
	[location(2), cond(HasColor)] color: vec4[f32],
	[location(3), cond(HasNormal)] normal: vec3[f32],
	[location(4), cond(HasNormalMapping)] tangent: vec3[f32],
	[builtin(position)] position: vec4[f32],
}

// Fragment stage
[cond(ForwardPass)]
struct FragOut
{
	[location(0)] RenderTarget0: vec4[f32],
	[builtin(frag_depth), cond(DistanceDepth)] fragdepth: f32,
}

[cond(!ForwardPass)]
alias FragOut = GBufferOutput;

[export]
fn ComputeColor(input: VertOut) -> vec4[f32]
{
	let color = settings.BaseColor;

	const if (HasUV)
		color.a *= TextureOverlay.Sample(input.uv).r;

	const if (HasColor)
		color *= input.color;

	const if (HasBaseColorTexture)
		color *= MaterialBaseColorMap.Sample(input.uv);

	const if (HasAlphaTexture)
		color.w *= MaterialAlphaMap.Sample(input.uv).x;

	const if (AlphaTest)
	{
		if (color.w < settings.AlphaThreshold)
			discard;
	}

	return color;
}

[cond(HasLighting)]
fn ComputeNormal(input: VertOut) -> vec3[f32]
{
	const if (HasNormalMapping)
	{
		let N = normalize(input.normal);
		let T = normalize(input.tangent);
		let B = cross(N, T);
		let tbnMatrix = mat3[f32](T, B, N);

		return normalize(tbnMatrix * (MaterialNormalMap.Sample(input.uv).xyz * 2.0 - vec3[f32](1.0, 1.0, 1.0)));
	}
	else
		return normalize(input.normal);
}

[export, cond(HasLighting)]
fn ComputeMetallic(input: VertOut) -> f32
{
	let metallic = settings.MetallicFactor;
	const if (HasMetallicTexture)
		metallic *= MaterialMetallicMap.Sample(input.uv).x;

	return metallic;
}

[export, cond(HasLighting)]
fn ComputeRoughness(input: VertOut) -> f32
{
	let roughness = settings.RoughnessFactor;
	const if (HasRoughnessTexture)
		roughness *= MaterialRoughnessMap.Sample(input.uv).x;

	return roughness;
}

[export, cond(HasLighting)]
fn ComputeLighting(color: vec3[f32], input: VertOut) -> vec3[f32]
{
	let lightRadiance = vec3[f32](0.0, 0.0, 0.0);

	let eyeVec = normalize(viewerData.eyePosition - input.worldPos);

	let albedo = color.xyz;
	let normal = ComputeNormal(input);
	let metallic = ComputeMetallic(input);
	let roughness = ComputeRoughness(input);

	let F0 = vec3[f32](0.04, 0.04, 0.04);
	F0 = lerp(F0, albedo, metallic.rrr);

	let albedoFactor = albedo / Pi;

	for lightIndex in u32(0) -> lightData.directionalLightCount
	{
		let light = lightData.directionalLights[lightIndex];
		let result = ComputeDirectionalLight(light, eyeVec, albedoFactor, F0, normal, metallic, roughness);

		let shadowFactor = 1.0;
		const if (EnableShadowMapping)
			shadowFactor = ComputeDirectionalLightShadow(light, shadowMapsDirectional[lightIndex], input.worldPos, result.lambert, viewerData.viewMatrix);

		lightRadiance += shadowFactor * result.radiance;
	}

	for lightIndex in u32(0) -> lightData.pointLightCount
	{
		let light = lightData.pointLights[lightIndex];
		let result = ComputePointLight(light, eyeVec, albedoFactor, F0, normal, metallic, roughness, input.worldPos);

		let shadowFactor = 1.0;
		const if (EnableShadowMapping)
			shadowFactor = ComputePointLightShadow(light, shadowMapsPoint[lightIndex], result.dist, result.lightToWorldDir);

		lightRadiance += shadowFactor * result.radiance;
	}

	for lightIndex in u32(0) -> lightData.spotLightCount
	{
		let light = lightData.spotLights[lightIndex];
		let result = ComputeSpotLight(light, eyeVec, albedoFactor, F0, normal, metallic, roughness, input.worldPos);

		let shadowFactor = 1.0;
		const if (EnableShadowMapping)
			shadowFactor = ComputeSpotLightShadow(light, shadowMapsSpot[lightIndex], input.worldPos, result.lambert);

		lightRadiance += shadowFactor * result.radiance;
	}

	let ambient = (0.03).rrr * albedo;

	let finalColor = ambient + lightRadiance;
	finalColor = finalColor / (finalColor + vec3[f32](1.0, 1.0, 1.0));

	return finalColor;
}

[export, entry(frag), cond(!DepthPass)]
fn FragMain(input: VertOut) -> FragOut
{
	let color = ComputeColor(input);

	const if (ForwardPass)
	{
		const if (HasLighting)
		{
			let normal = ComputeNormal(input);
			color.rgb = ComputeLighting(color.rgb, input, normal);
		}
	
		let output: FragOut;
		output.color = color;
	
		return output;
	}
	else
	{
		let normal = ComputeNormal(input);
		let metalness = ComputeMetallic(input);
		let roughness = ComputeRoughness(input);

		let output: FragOut;
		output.albedo = vec4[f32](color.rgb, metalness);
		output.normal = vec4[f32](EncodeNormal(normal), roughness);

		return output;
	}

	let output: FragOut;
	output.RenderTarget0 = color;
	return output;
}

// Shadow passes entries
[entry(frag), cond(DepthPass && DistanceDepth)]
[depth_write(replace)]
fn FragDepthDist(input: VertOut) -> FragOut
{
	let color = ComputeColor(input);

	let output: FragOut;
	output.RenderTarget0 = color;

	let dist = distance(viewerData.eyePosition, input.worldPos);
	output.fragdepth = dist / viewerData.farPlane;

	return output;
}

[entry(frag), cond(DepthPass && AlphaTest && !DistanceDepth)]
fn FragDepth(input: VertOut) -> FragOut
{
	let color = ComputeColor(input);

	let output: FragOut;
	output.RenderTarget0 = color;
	return output;
}

[entry(frag), cond(DepthPass && !AlphaTest && !DistanceDepth)]
fn FragDepthNoAlpha() {} //< dummy

// Vertex stage
[cond(!Billboard)]
struct VertIn
{
	[location(VertexPositionLoc)]
	pos: vec3[f32],

	[cond(HasVertexColor), location(VertexColorLoc)]
	color: vec4[f32],

	[cond(HasVertexUV), location(VertexUvLoc)]
	uv: vec2[f32],

	[cond(HasNormal), location(VertexNormalLoc)]
	normal: vec3[f32],

	[cond(HasTangent), location(VertexTangentLoc)]
	tangent: vec3[f32],

	[cond(HasSkinning), location(VertexJointIndicesLoc)]
	jointIndices: vec4[i32],

	[cond(HasSkinning), location(VertexJointWeightsLoc)]
	jointWeights: vec4[f32]
}

[cond(Billboard)]
struct BillboardVertIn
{
	[builtin(vertex_index)] vertIndex: i32,

	[location(VertexSizeRotLocation)]
	sizeRot: vec4[f32], //< width,height,sin,cos

	[cond(HasVertexUV), location(VertexUvLoc)]
	uv: vec2[f32],

	[cond(HasVertexColor), location(VertexColorLoc)]
	color: vec4[f32]
}

const billboardPos = array[vec2[f32]](
	vec2[f32](-0.5, -0.5),
	vec2[f32]( 0.5, -0.5),
	vec2[f32](-0.5, 0.5),
	vec2[f32]( 0.5, 0.5)
);

[entry(vert), cond(Billboard)]
fn VertBillboard(input: BillboardVertIn) -> VertOut
{
	let position = billboardPos[input.vertIndex % 4];

	let size = input.sizeRot.xy;
	let sinCos = input.sizeRot.zw;

	let rotatedPosition = vec2[f32](
		position.x * sinCos.y - position.y * sinCos.x,
		position.y * sinCos.y + position.x * sinCos.x
	);
	rotatedPosition *= size;

	let cameraRight = vec3[f32](viewerData.viewMatrix[0][0], viewerData.viewMatrix[1][0], viewerData.viewMatrix[2][0]);
	let cameraUp = vec3[f32](viewerData.viewMatrix[0][1], viewerData.viewMatrix[1][1], viewerData.viewMatrix[2][1]);

	let worldPosition = vec3[f32](instanceData.worldMatrix[3].xyz);
	worldPosition += cameraRight * rotatedPosition.x;
	worldPosition += cameraUp * rotatedPosition.y;

	let output: VertOut;
	output.worldPos = worldPosition;
	output.position = viewerData.viewProjMatrix * vec4[f32](worldPosition, 1.0);

	const if (HasVertexUV)
		output.uv = input.uv;

	const if (HasColor)
		output.color = input.color;

	return output;
}

[entry(vert), cond(!Billboard)]
fn VertMain(input: VertIn) -> VertOut
{
	let pos: vec3[f32];
	const if (HasNormal) let normal: vec3[f32];

	const if (HasSkinning)
	{
		let jointMatrices = array[mat4[f32]](
			skeletalData.jointMatrices[input.jointIndices[0]],
			skeletalData.jointMatrices[input.jointIndices[1]],
			skeletalData.jointMatrices[input.jointIndices[2]],
			skeletalData.jointMatrices[input.jointIndices[3]]
		);

		const if (HasNormal)
		{
			let skinningOutput = SkinLinearPositionNormal(jointMatrices, input.jointWeights, input.pos, input.normal);
			pos = skinningOutput.position;
			normal = skinningOutput.normal;
		}
		else
		{
			let skinningOutput = SkinLinearPosition(jointMatrices, input.jointWeights, input.pos);
			pos = skinningOutput.position;
		}
	}
	else
	{
		pos = input.pos;
		const if (HasNormal)
			normal = input.normal;
	}

	const if (ShadowPass)
	{
		pos *= settings.ShadowPosScale;
		const if (HasNormal)
			pos -= normal * settings.ShadowMapNormalOffset;
	}

	let worldPosition = instanceData.worldMatrix * vec4[f32](pos, 1.0);

	let output: VertOut;
	output.worldPos = worldPosition.xyz;
	output.position = viewerData.viewProjMatrix * worldPosition;

	const if (HasNormal || HasNormalMapping) let rotationMatrix = transpose(mat3[f32](instanceData.invWorldMatrix));

	const if (HasColor)
		output.color = input.color;

	const if (HasNormal)
		output.normal = rotationMatrix * input.normal;

	const if (HasVertexUV)
		output.uv = input.uv;

	const if (HasNormalMapping)
		output.tangent = rotationMatrix * input.tangent;

	return output;
}
