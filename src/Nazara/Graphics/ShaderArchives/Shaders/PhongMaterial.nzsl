[nzsl_version("1.1")]
module Material.Phong;

import InstanceData from Engine.InstanceData;
import LightData from Lighting.LightData;
import SkeletalData from Engine.SkeletalData;
import ViewerData from Engine.ViewerData;

import * from Lighting.Phong;
import * from Lighting.Shadow;
import SkinLinearPosition, SkinLinearPositionNormal from Engine.SkinningLinear;
import GBufferOutput from DeferredShading.PhongGBuffer;
import * from DeferredShading.NormalEncoding;
import EncodeNormal from DeferredShading.NormalEncoding;

// Pass-specific options
option DepthPass: bool = false;
option DistanceDepth: bool = false;
option ForwardPass: bool = true;
option ShadowPass: bool = false;

// Basic material options
option HasBaseColorTexture: bool = false;
option HasAlphaTexture: bool = false;
option AlphaTest: bool = false;

// Phong material options
option HasEmissiveTexture: bool = false;
option HasHeightTexture: bool = false;
option HasNormalTexture: bool = false;
option HasSpecularTexture: bool = false;

// Billboard related options
option Billboard: bool = false;

// Vertex declaration related options
const InvalidLoc = u32.Max;

option VertexColorLoc: u32 = InvalidLoc;
option VertexNormalLoc: u32 = InvalidLoc;
option VertexPositionLoc: u32 = InvalidLoc;
option VertexSizeRotLocation: u32 = InvalidLoc;
option VertexTangentLoc: u32 = InvalidLoc;
option VertexUvLoc: u32 = InvalidLoc;

option VertexJointIndicesLoc: u32 = InvalidLoc;
option VertexJointWeightsLoc: u32 = InvalidLoc;

option EnableShadowMapping: bool = true;
option MaxLightCount: u32 = 3;

const HasNormal = (VertexNormalLoc != InvalidLoc);
const HasVertexColor = (VertexColorLoc != InvalidLoc);
const HasColor = (HasVertexColor || Billboard);
const HasTangent = (VertexTangentLoc != InvalidLoc);
const HasVertexUV = (VertexUvLoc != InvalidLoc);
const HasUV = (HasVertexUV);
const HasNormalMapping = HasNormalTexture && HasNormal && HasTangent && !DepthPass;
const HasSkinning = (VertexJointIndicesLoc != InvalidLoc && VertexJointWeightsLoc != InvalidLoc);
const HasLighting = HasNormal && !DepthPass;

[layout(std140)]
struct MaterialSettings
{
	// Basic settings
	[tag("AlphaTestThreshold")]
	AlphaThreshold: f32,

	[tag("ShadowMapNormalOffset")]
	ShadowMapNormalOffset: f32,

	[tag("ShadowPosScale")]
	ShadowPosScale: f32,

	[tag("BaseColor")]
	BaseColor: vec4[f32],

	// Phong settings
	[tag("AmbientColor")]
	AmbientColor: vec4[f32], //< TODO: Switch to vec3[f32]

	[tag("SpecularColor")]
	SpecularColor: vec4[f32], //< TODO: Switch to vec3[f32

	[tag("Shininess")]
	Shininess: f32
}

[tag("Material")]
[auto_binding]
external
{
	[tag("Settings")] settings: uniform[MaterialSettings],
	[tag("BaseColorMap")] MaterialBaseColorMap: sampler2D[f32],
	[tag("AlphaMap")] MaterialAlphaMap: sampler2D[f32],
	[tag("EmissiveMap")] MaterialEmissiveMap: sampler2D[f32],
	[tag("HeightMap")] MaterialHeightMap: sampler2D[f32],
	[tag("NormalMap")] MaterialNormalMap: sampler2D[f32],
	[tag("SpecularMap")] MaterialSpecularMap: sampler2D[f32],
}

[tag("Engine")]
[auto_binding]
external
{
	[tag("TextureOverlay")] TextureOverlay: sampler2D[f32],
	[tag("InstanceData")] instanceData: uniform[InstanceData],
	[tag("ViewerData")] viewerData: uniform[ViewerData],
	[tag("SkeletalData")] skeletalData: uniform[SkeletalData],
	[tag("LightData")/*, cond(ForwardPass)*/] lightData: uniform[LightData],
	[tag("ShadowMapsDirectional")] shadowMapsDirectional: array[depth_sampler2D_array[f32], MaxLightCount],
	[tag("ShadowMapsPoint")] shadowMapsPoint: array[sampler_cube[f32], MaxLightCount],
	[tag("ShadowMapsSpot")] shadowMapsSpot: array[depth_sampler2D[f32], MaxLightCount],
}

struct VertOut
{
	[location(0)] worldPos: vec3[f32],
	[location(1), cond(HasUV)] uv: vec2[f32],
	[location(2), cond(HasColor)] color: vec4[f32],
	[location(3), cond(HasNormal)] normal: vec3[f32],
	[location(4), cond(HasNormalMapping)] tangent: vec3[f32],
	[builtin(position)] position: vec4[f32],
	[builtin(frag_coord)] fragcoord: vec4[f32]
}

// Fragment stage
[cond(ForwardPass)]
struct FragOut
{
	[location(0)] color: vec4[f32],
	[builtin(frag_depth), cond(DistanceDepth)] fragdepth: f32,
}

[cond(!ForwardPass)]
alias FragOut = GBufferOutput;

fn ComputeColor(input: VertOut) -> vec4[f32]
{
	let color = settings.BaseColor;

	const if (HasUV)
		color.a *= TextureOverlay.Sample(input.uv).r;

	const if (HasColor)
		color *= input.color;

	const if (HasBaseColorTexture)
		color *= MaterialBaseColorMap.Sample(input.uv);

	const if (HasAlphaTexture)
		color.w *= MaterialAlphaMap.Sample(input.uv).x;

	const if (AlphaTest)
	{
		if (color.w < settings.AlphaThreshold)
			discard;
	}

	return color;
}

[cond(HasLighting)]
fn ComputeNormal(input: VertOut) -> vec3[f32]
{
	const if (HasNormalMapping)
	{
		let N = normalize(input.normal);
		let T = normalize(input.tangent);
		let B = cross(N, T);
		let tbnMatrix = mat3[f32](T, B, N);

		return normalize(tbnMatrix * (MaterialNormalMap.Sample(input.uv).xyz * 2.0 - vec3[f32](1.0, 1.0, 1.0)));
	}
	else
		return normalize(input.normal);
}

[cond(HasLighting)]
fn ComputeLighting(color: vec3[f32], input: VertOut) -> vec3[f32]
{
	let lightAmbient = vec3[f32](0.0, 0.0, 0.0);
	let lightDiffuse = vec3[f32](0.0, 0.0, 0.0);
	let lightSpecular = vec3[f32](0.0, 0.0, 0.0);

	let eyeVec = normalize(viewerData.eyePosition - input.worldPos);

	let normal = ComputeNormal(input);

	for lightIndex in 0 -> lightData.directionalLightCount
	{
		let light = lightData.directionalLights[lightIndex];
		let result = ComputeDirectionalLight(light, eyeVec, normal, settings.Shininess);

		let shadowFactor = 1.0;
		const if (EnableShadowMapping)
			shadowFactor = ComputeDirectionalLightShadow(light, shadowMapsDirectional[lightIndex], input.worldPos, result.lambert, viewerData.viewMatrix);

		lightAmbient += result.ambient;
		lightDiffuse += shadowFactor * result.diffuse;
		lightSpecular += shadowFactor * result.specular;
	}

	for lightIndex in 0 -> lightData.pointLightCount
	{
		let light = lightData.pointLights[lightIndex];
		let result = ComputePointLight(light, eyeVec, normal, settings.Shininess, input.worldPos);

		let shadowFactor = 1.0;
		const if (EnableShadowMapping)
			shadowFactor = ComputePointLightShadow(light, shadowMapsPoint[lightIndex], result.dist, result.lightToWorldDir);

		lightAmbient += result.ambient;
		lightDiffuse += shadowFactor * result.diffuse;
		lightSpecular += shadowFactor * result.specular;
	}

	for lightIndex in 0 -> lightData.spotLightCount
	{
		let light = lightData.spotLights[lightIndex];
		let result = ComputeSpotLight(light, eyeVec, normal, settings.Shininess, input.worldPos);

		let shadowFactor = 1.0;
		const if (EnableShadowMapping)
			shadowFactor = ComputeSpotLightShadow(light, shadowMapsSpot[lightIndex], input.worldPos, result.lambert);

		lightAmbient += result.ambient;
		lightDiffuse += shadowFactor * result.diffuse;
		lightSpecular += shadowFactor * result.specular;
	}

	lightAmbient *= settings.AmbientColor.rgb;
	lightSpecular *= settings.SpecularColor.rgb;

	const if (HasSpecularTexture)
		lightSpecular *= MaterialSpecularMap.Sample(input.uv).rgb;

	let lightColor = lightAmbient + lightDiffuse + lightSpecular;
	return color * lightColor;
}

[entry(frag), cond(!DepthPass)]
fn FragMain(input: VertOut) -> FragOut
{
	let color = ComputeColor(input);

	const if (ForwardPass)
	{
		const if (HasLighting)
			color.rgb = ComputeLighting(color.rgb, input);
	
		let output: FragOut;
		output.color = color;
	
		return output;
	}
	else
	{
		let normal = ComputeNormal(input);

		let output: FragOut;
		output.albedo = color;
		output.normal = vec4[f32](EncodeNormal(normal), 1.0);

		return output;
	}
}

// Shadow passes entries
[entry(frag), cond(DepthPass && DistanceDepth)]
[depth_write(replace)]
fn FragDepthDist(input: VertOut) -> FragOut
{
	let color = ComputeColor(input);

	let output: FragOut;
	output.color = color;

	let dist = distance(viewerData.eyePosition, input.worldPos);
	output.fragdepth = dist / viewerData.farPlane;

	return output;
}

[entry(frag), cond(DepthPass && AlphaTest && !DistanceDepth)]
fn FragDepth(input: VertOut) -> FragOut
{
	let color = ComputeColor(input);

	let output: FragOut;
	output.color = color;
	return output;
}

[entry(frag), cond(DepthPass && !AlphaTest && !DistanceDepth)]
fn FragDepthNoAlpha() {} //< dummy

// Vertex stage
[cond(!Billboard)]
struct VertIn
{
	[location(VertexPositionLoc)]
	pos: vec3[f32],

	[cond(HasVertexColor), location(VertexColorLoc)]
	color: vec4[f32],

	[cond(HasVertexUV), location(VertexUvLoc)]
	uv: vec2[f32],

	[cond(HasNormal), location(VertexNormalLoc)]
	normal: vec3[f32],

	[cond(HasTangent), location(VertexTangentLoc)]
	tangent: vec3[f32],

	[cond(HasSkinning), location(VertexJointIndicesLoc)]
	jointIndices: vec4[i32],

	[cond(HasSkinning), location(VertexJointWeightsLoc)]
	jointWeights: vec4[f32]
}

[cond(Billboard)]
struct BillboardVertIn
{
	[builtin(vertex_index)] vertIndex: i32,

	[location(VertexSizeRotLocation)]
	sizeRot: vec4[f32], //< width,height,sin,cos

	[cond(HasVertexUV), location(VertexUvLoc)]
	uv: vec2[f32],

	[cond(HasVertexColor), location(VertexColorLoc)]
	color: vec4[f32]
}

const billboardPos = array[vec2[f32]](
	vec2[f32](-0.5, -0.5),
	vec2[f32]( 0.5, -0.5),
	vec2[f32](-0.5, 0.5),
	vec2[f32]( 0.5, 0.5)
);

[entry(vert), cond(Billboard)]
fn VertBillboard(input: BillboardVertIn) -> VertOut
{
	let position = billboardPos[input.vertIndex % 4];

	let size = input.sizeRot.xy;
	let sinCos = input.sizeRot.zw;

	let rotatedPosition = vec2[f32](
		position.x * sinCos.y - position.y * sinCos.x,
		position.y * sinCos.y + position.x * sinCos.x
	);
	rotatedPosition *= size;

	let cameraRight = vec3[f32](viewerData.viewMatrix[0][0], viewerData.viewMatrix[1][0], viewerData.viewMatrix[2][0]);
	let cameraUp = vec3[f32](viewerData.viewMatrix[0][1], viewerData.viewMatrix[1][1], viewerData.viewMatrix[2][1]);

	let worldPosition = vec3[f32](instanceData.worldMatrix[3].xyz);
	worldPosition += cameraRight * rotatedPosition.x;
	worldPosition += cameraUp * rotatedPosition.y;

	let output: VertOut;
	output.worldPos = worldPosition;
	output.position = viewerData.viewProjMatrix * vec4[f32](worldPosition, 1.0);

	const if (HasVertexUV)
		output.uv = input.uv;

	const if (HasColor)
		output.color = input.color;

	return output;
}

[entry(vert), cond(!Billboard)]
fn main(input: VertIn) -> VertOut
{
	let pos: vec3[f32];
	const if (HasNormal) let normal: vec3[f32];

	const if (HasSkinning)
	{
		let jointMatrices = array[mat4[f32]](
			skeletalData.jointMatrices[input.jointIndices[0]],
			skeletalData.jointMatrices[input.jointIndices[1]],
			skeletalData.jointMatrices[input.jointIndices[2]],
			skeletalData.jointMatrices[input.jointIndices[3]]
		);

		const if (HasNormal)
		{
			let skinningOutput = SkinLinearPositionNormal(jointMatrices, input.jointWeights, input.pos, input.normal);
			pos = skinningOutput.position;
			normal = skinningOutput.normal;
		}
		else
		{
			let skinningOutput = SkinLinearPosition(jointMatrices, input.jointWeights, input.pos);
			pos = skinningOutput.position;
		}
	}
	else
	{
		pos = input.pos;
		const if (HasNormal)
			normal = input.normal;
	}

	const if (ShadowPass)
	{
		pos *= settings.ShadowPosScale;
		const if (HasNormal)
			pos -= normal * settings.ShadowMapNormalOffset;
	}

	let worldPosition = instanceData.worldMatrix * vec4[f32](pos, 1.0);

	let output: VertOut;
	output.worldPos = worldPosition.xyz;
	output.position = viewerData.viewProjMatrix * worldPosition;

	const if (HasNormal || HasNormalMapping) let rotationMatrix = transpose(mat3[f32](instanceData.invWorldMatrix));

	const if (HasColor)
		output.color = input.color;

	const if (HasNormal)
		output.normal = rotationMatrix * normal;

	const if (HasVertexUV)
		output.uv = input.uv;

	const if (HasNormalMapping)
		output.tangent = rotationMatrix * input.tangent;

	return output;
}
